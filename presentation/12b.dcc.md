# DCC as a flake

```nix
{
  description = "DCC - The Debugging C/C++ Compiler";

  inputs = {
    # Grab our dependencies from v25.05
    nixpkgs.url = "github:nixos/nixpkgs/nixos-25.05";
  };

  outputs =
    { self, nixpkgs }:
    let
      # Systems supported
      allSystems = [
        "x86_64-linux" # 64-bit Intel/AMD Linux
        "aarch64-linux" # 64-bit ARM Linux
        "x86_64-darwin" # 64-bit Intel macOS
        "aarch64-darwin" # 64-bit ARM macOS
      ];

      # Helper to provide system-specific attributes
      forAllSystems =
        f:
        nixpkgs.lib.genAttrs allSystems (
          system:
          f {
            system = system;
            pkgs = import nixpkgs { inherit system; };
          }
        );
    in
    {
      packages = forAllSystems (
        { pkgs, ... }:
        {
          # dcc
          default =
            let
              binName = "dcc";
              cDependencies = with pkgs; [
                # Build-time dependencies
                gcc
                clang
                python3
                gdb
                valgrind
                gnumake
                git
                zip
                which
              ];
            in
            pkgs.stdenv.mkDerivation {
              name = binName;
              src = self;
              buildInputs = cDependencies;
              nativeBuildInputs = [ pkgs.makeWrapper ];
              buildPhase = "make";
              installPhase = ''
                mkdir -p $out/bin
                cp ${binName} $out/bin/${binName}
                ln -s $out/bin/${binName} $out/bin/d++
                ln -s $out/bin/${binName} $out/bin/dcc++
                # Compiled executable runtime dependencies (copying them like
                # this is hacky but it works)
                cp "$(which gdb)" $out/bin/gdb
                cp "$(which valgrind)" $out/bin/valgrind
                wrapProgram $out/bin/dcc \
                  --inherit-argv0 \
                  --prefix PATH : "${
                    # Compiler runtime dependencies
                    pkgs.lib.makeBinPath [
                      pkgs.gcc
                      pkgs.clang
                      pkgs.python3
                      pkgs.gdb
                      pkgs.valgrind
                    ]
                  }"
              '';
            };
        }
      );

      devShells = forAllSystems (
        { system, pkgs }:
        {
          default = pkgs.mkShell {
            packages = [ self.packages.${system}.default ];
          };
        }
      );
    };
}
```
